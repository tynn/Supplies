buildscript {
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.1'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.0'
    }
}

ext {
    androidSupportVersion = '26.1.+'
    firebaseVersion = '11.8.+'
}

subprojects {
    repositories {
        google()
        jcenter()
    }

    apply plugin: 'com.android.library'
    apply plugin: 'com.github.dcendents.android-maven'

    android {
        compileSdkVersion 26
        buildToolsVersion '26.0.3'

        defaultConfig {
            targetSdkVersion 26
            testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        }

        dataBinding {
            addDefaultAdapters false
            enabled false
        }
    }

    conf2ScopeMappings.addMapping 200, configurations.api, 'provided'
    conf2ScopeMappings.addMapping 300, configurations.implementation, 'provided'
    conf2ScopeMappings.addMapping 400, configurations.compileOnly, 'provided'

    dependencies {
        compileOnly "com.android.support:support-annotations:$project.androidSupportVersion"
        testImplementation 'junit:junit:4.12'
        androidTestImplementation 'com.android.support.test:runner:1.0.1'
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java.srcDirs
    }

    task javadoc(type: Javadoc) {
        failOnError false
        source = android.sourceSets.main.java.sourceFiles
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
