buildscript {
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.2'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
    }
}

ext {
    androidSupportVersion = '26.1.+'
    firebaseVersion = '11.8.+'
}

subprojects {
    repositories {
        google()
        jcenter()
    }

    apply plugin: 'com.android.library'
    apply plugin: 'com.github.dcendents.android-maven'

    android {
        compileSdkVersion 29

        defaultConfig {
            targetSdkVersion 29
            testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        }

        dataBinding {
            enabled false
        }
    }

    conf2ScopeMappings.addMapping 200, configurations.api, 'provided'
    conf2ScopeMappings.addMapping 300, configurations.implementation, 'provided'
    conf2ScopeMappings.addMapping 400, configurations.compileOnly, 'provided'

    dependencies {
        implementation 'androidx.annotation:annotation:1.1.0'
        testImplementation 'junit:junit:4.12'
        androidTestImplementation 'androidx.test:runner:1.2.0'
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java.srcDirs
    }

    task javadoc(type: Javadoc) {
        failOnError false
        source = android.sourceSets.main.java.sourceFiles
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
